//=============================================================================
// Caltech CS130 - Winter 2023

%import common.WS
%ignore WS

//========================================
// Top-level formulas and expressions

?formula : "=" expression

?expression : cmp_expr

//========================================
// Arithmetic expressions

// This generates right-deep trees:  ?add_expr : mul_expr (ADD_OP add_expr)?
// Instead we would prefer to generate left-deep trees when given a sequence
// of operators at the same precedence level.
?cmp_expr : (add_expr CMP_OP)? add_expr

?add_expr : (add_expr ADD_OP)? mul_expr

?mul_expr : (mul_expr MUL_OP)? unary_op

?unary_op : ADD_OP? concat_expr

func_expr : FUNCTION "(" ((expression ",")* expression)? ")"

//========================================
// String concatenation

?concat_expr : (concat_expr "&")? base

//========================================
// Base values

?base : cell
      | cell_range
      | func_expr
      | ERROR_VALUE             -> error
      | NUMBER                  -> number
      | STRING                  -> string
      | BOOL                    -> boolean
      | "(" expression ")"      -> parens

cell : CELLREF
cell_range : cell ":" cell

//========================================
// Lexer terminals

// Specify the operators as separately named lexer rules so that they aren't
// discarded from the resulting parse tree.

CMP_OP: (">" | "<" | "=" | "<>" | ">=" | "<=" | "==" | "!=")
ADD_OP: ("+" | "-")
MUL_OP: ("*" | "/")

// Error values are also separately named lexer rules so we know what kind of
// error is referenced.  All are case-insensitive.

ERROR_VALUE: ("#ERROR!"i | "#CIRCREF!"i | "#REF!"i | "#NAME?"i | "#VALUE!"i | "#DIV/0!"i)

// Lexer rules for different kinds of terminals

CELLREF: /(([A-Za-z_][A-Za-z0-9_]*|'[^']*')!)?\$?[A-Za-z]+\$?[0-9]+/

// Don't need to support signs on numbers because we have unary +/- operator
// support in the parser.
NUMBER: /([0-9]+(\.[0-9]*)?)|(\.[0-9]+)/

STRING: /\"[^"]*\"/

BOOL: ("true"i | "false"i)

FUNCTION: /[A-Za-z][A-Za-z0-9_]*/